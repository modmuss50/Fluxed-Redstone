buildscript {
    ext.kotlin_version = '1.0.0-rc-1036'
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    shadow
    compile.extendsFrom shadow
}

repositories {
    maven {
        name = "Modmuss50"
        url = "http://maven.modmuss50.me/"
    }
    maven {
        name = "Amadornes"
        url "http://maven.amadornes.com/"
    }
    mavenCentral()
}

version = "1.2.0"

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

minecraft {
    version = "1.8.9-11.15.1.1741"
    mappings = 'snapshot_20160103'
    replace "@MODVERSION@", project.version
    makeObfSourceJar = false
    useDepAts = true
    runDir = "run"
}

group = 'FluxedRestone'

dependencies {
    compile 'RebornCore:RebornCore-1.8.9:1.4.2.+:dev'
    deobfCompile 'MCMultiPart:MCMultiPart:1.0.6:universal'
    shadow "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    shadow "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'io.drakon.forgelinFR.CorePlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}


jar.dependsOn shadowJar
jar.enabled = false

shadowJar {
    archiveName = tasks.jar.archiveName
    configurations = [project.configurations.shadow]
    relocate 'kotlin', 'me.modmuss50.fr.repack.kotlin'
    relocate 'org.jetbrains', 'me.modmuss50.fr.repack.org.jetbrains'
	    manifest {
        attributes 'FMLCorePlugin': 'io.drakon.forgelinFR.CorePlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

reobfJar {
    setJar(tasks.shadowJar.archivePath)
}
reobfJar.dependsOn shadowJar


uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:///var/www/maven/")
            pom {
                groupId = "FluxedRestone"
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'FluxedRestone'
                    url 'https://github.com/modmuss50/Fluxed-Redstone'
                    scm {
                        url 'https://github.com/modmuss50/Fluxed-Redstone'
                        connection 'scm:git:git@github.com:modmuss50/Fluxed-Redstone.git'
                        developerConnection 'scm:git:git@github.com:modmuss50/Fluxed-Redstone.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/modmuss50/Fluxed-Redstone/issues'
                    }
                    licenses {
                        license {
                            name 'License'
                            url 'https://raw.githubusercontent.com/modmuss50/Fluxed-Redstone/master/LICENSE.md'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'modmuss50'
                            name 'modmuss50'
                            roles {
                                role 'developer'
                            }
                        }
                    }
                }
            }
        }
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
    manifest {
        attributes 'FMLCorePlugin': 'io.drakon.forgelinFR.CorePlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

artifacts {
    archives deobfJar
}

build.dependsOn deobfJar

task wrapper(type: Wrapper) {
    //To update the wrapper change the version bellow then run: gradle wrapper
    gradleVersion = '2.9'
}
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
